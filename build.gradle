plugins {
    id 'java'
    id 'scala'
    id 'checkstyle'
    id 'com.github.alisiikh.scalastyle' version '3.3.1'
    id 'org.jetbrains.intellij' version '0.4.21'
    id 'org.sonarqube' version '3.0'
    id 'jacoco'
    id 'maven-publish'
}

group 'fi.aalto.cs'
version '2.0'

sourceCompatibility = 1.8
def remoteRobotVersion = '0.10.3'
def remoteRobotTestFilter = 'fi.aalto.cs.apluscourses.remoterobot.*'

repositories {
    mavenCentral()
    jcenter()
    maven { url = "https://jetbrains.bintray.com/intellij-third-party-dependencies" }
}

dependencies {
    compile group: 'commons-io', name: 'commons-io', version: '2.6'
    compile group: 'net.lingala.zip4j', name: 'zip4j', version: '2.6.3'
    compileOnly group: 'org.scala-lang', name: 'scala-library', version: '2.13.4'
    implementation group: 'org.json', name: 'json', version: '20190722'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.hamcrest', name: 'hamcrest', version: '2.2'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '3.2.4'
    testCompile group: 'org.powermock', name: 'powermock-module-junit4', version: '2.0.2'
    testCompile group: 'org.powermock', name: 'powermock-api-mockito2', version: '2.0.2'
    testCompile group: 'org.scala-lang', name: 'scala-library', version: '2.13.4'
    testCompile('io.rest-assured:rest-assured:4.2.0') {
        exclude group:'org.codehaus.groovy', module: 'groovy-xml'
    }
    testImplementation("com.intellij.remoterobot:remote-robot:$remoteRobotVersion")
}

intellij {
    //  this is the version of the IntelliJ IDEA API (SDK) used to build the plugin
    version '2020.3'
    plugins = ['java', 'org.intellij.scala:2020.3.16']
    downloadRobotServerPlugin.version = remoteRobotVersion
}

publishPlugin {
    token = System.getenv('INTELLIJ_PUBLISH_TOKEN')
    channels = [System.getenv('INTELLIJ_PUBLISH_CHANNEL')]
}

checkstyle {
    configFile = file('checkstyle/google_checks.xml')
    maxWarnings = 0
}

scalastyle {
    config = file('scalastyle/scalastyle-checks.xml')
    scalaVersion = '2.13'
    failOnWarning = true
    sourceSets {
        test {
            skip = true
        }
    }
}

test {
    filter {
        excludeTestsMatching remoteRobotTestFilter
    }
}

task uiTest(type: Test) {
    filter {
        includeTestsMatching remoteRobotTestFilter
    }
}

task gatherBuildInfo(dependsOn: processResources) {
    doLast {
        new File("$buildDir/resources/main/build-info.properties").withWriter { writer ->
            Properties props = new Properties()
            props['version'] = version
            props.store(writer, null)
        }
    }
}

classes.dependsOn gatherBuildInfo

check.dependsOn jacocoTestReport

jacocoTestReport {
    reports.xml.enabled true
}
