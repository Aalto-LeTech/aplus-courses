plugins {
    id 'java'
    id 'checkstyle'
    id 'org.jetbrains.intellij' version '0.4.15'
    id "org.sonarqube" version "2.8"
    id 'jacoco'
    id 'maven-publish'
}

group 'fi.aalto.cs'
version '0.1.' + (System.getenv('TRAVIS_BUILD_NUMBER') ?: '0')

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'commons-io', name: 'commons-io', version: '2.6'
    compile group: 'net.lingala.zip4j', name: 'zip4j', version: '2.3.0'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.hamcrest', name: 'hamcrest', version: '2.2'
    implementation group: 'org.json', name: 'json', version: '20190722'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '3.2.4'
}

intellij {
    version '2019.3.2'
    plugins = ['java']
}

publishPlugin {
    token = System.getenv('INTELLIJ_PUBLISH_TOKEN')
    channels = [System.getenv('INTELLIJ_PUBLISH_CHANNEL')]
}

checkstyle {
    configFile = file('checkstyle/google_checks.xml')
    maxWarnings = 0
}

task gatherBuildInfo(dependsOn: processResources) {
    doLast {
        new File("$buildDir/resources/main/build-info.properties").withWriter { writer ->
            Properties props = new Properties()
            props['version'] = version;
            props.store(writer, null);
        }
    }
}

classes.dependsOn gatherBuildInfo

check.dependsOn jacocoTestReport

jacocoTestReport {
    reports.xml.enabled true
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/Aalto-LeTech/intellij-plugin")
            credentials {
                username = System.getenv("GRP_USER")
                password = System.getenv("GRP_KEY")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}
